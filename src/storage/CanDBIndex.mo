import RBT "mo:stable-rbtree/StableRBTree";
import Cycles "mo:base/ExperimentalCycles";
import Debug "mo:base/Debug";
import Text "mo:base/Text";
import TrieSet "mo:base/TrieSet";
import CA "mo:candb/CanisterActions";
import Utils "mo:candb/Utils";
import CanisterMap "mo:candb/CanisterMap";
import Buffer "mo:stable-buffer/StableBuffer";
import CanDBPartition "CanDBPartition";
import CanDBPartition2 "../storage/CanDBPartition";
import Admin "mo:candb/CanDBAdmin";
import Principal "mo:base/Principal";
import Hash "mo:base/Hash";
import Array "mo:base/Array";
import Int "mo:base/Int";
import Iter "mo:base/Iter";
import CanDB "mo:candb/CanDB";
import Entity "mo:candb/Entity";
import Canister "mo:matchers/Canister";

shared({caller = initialOwner}) actor class CanDBIndex() = this {
  stable var owners: [Principal] = [initialOwner];

  stable var initialized: Bool = false;

  public shared func init(_owners: [Principal]): async () {
    if (initialized) {
      Debug.trap("already initialized");
    };

    owners := _owners;
    ignore await* createStorageCanister("main", ownersOrSelf());
    ignore await* createStorageCanister("user", ownersOrSelf()); // user data

    initialized := true;
  };

  func checkCaller(caller: Principal) {
    if (Array.find(owners, func(e: Principal): Bool { e == caller; }) == null) {
      Debug.trap("CanDBIndex: not allowed");
    }
  };

  public shared({caller = caller}) func setOwners(_owners: [Principal]): async () {
    checkCaller(caller); // FIXME: too weak

    owners := _owners;
  };

  public query func getOwners(): async [Principal] { owners };

  func ownersOrSelf(): [Principal] {
    let buf = Buffer.fromArray<Principal>(owners);
    Buffer.add(buf, Principal.fromActor(this));
    Buffer.toArray(buf);
  };

  let maxSize = #heapSize(500_000_000);

  stable var pkToCanisterMap = CanisterMap.init();

  /// @required API (Do not delete or change)
  ///
  /// Get all canisters for an specific PK
  ///
  /// This method is called often by the candb-client query & update methods. 
  public shared query({caller}) func getCanistersByPK(pk: Text): async [Text] {
    getCanisterIdsIfExists(pk);
  };
  
  /// @required function (Do not delete or change)
  ///
  /// Helper method acting as an interface for returning an empty array if no canisters
  /// exist for the given PK
  func getCanisterIdsIfExists(pk: Text): [Text] {
    switch(CanisterMap.get(pkToCanisterMap, pk)) {
      case null { [] };
      case (?canisterIdsBuffer) { Buffer.toArray(canisterIdsBuffer) } 
    }
  };

  /// This hook is called by CanDB for AutoScaling the User Service Actor.
  ///
  /// If the developer does not spin up an additional User canister in the same partition within this method, auto-scaling will NOT work
  /// Upgrade user canisters in a PK range, i.e. rolling upgrades (limit is fixed at upgrading the canisters of 5 PKs per call)
  public shared({caller}) func upgradeAllPartitionCanisters(wasmModule: Blob): async Admin.UpgradePKRangeResult {
    checkCaller(caller);

    await Admin.upgradeCanistersInPKRange({
      canisterMap = pkToCanisterMap;
      lowerPK = "";
      upperPK = "\u{FFFF}";
      limit = 5;
      wasmModule = wasmModule;
      scalingOptions = {
        autoScalingHook = autoScaleCanister;
        sizeLimit = maxSize;
      };
      owners = ?ownersOrSelf();
    });
  };

  public shared({caller}) func autoScaleCanister(pk: Text): async Text {
    checkCaller(caller);

    if (Utils.callingCanisterOwnsPK(caller, pkToCanisterMap, pk)) {
      await* createStorageCanister(pk, ownersOrSelf());
    } else {
      Debug.trap("error, called by non-controller=" # debug_show(caller));
    };
  };

  func createStorageCanister(pk: Text, controllers: [Principal]): async* Text {
    Debug.print("creating new storage canister with pk=" # pk);
    // Pre-load 300 billion cycles for the creation of a new storage canister
    // Note that canister creation costs 100 billion cycles, meaning there are 200 billion
    // left over for the new canister when it is created
    Cycles.add(300_000_000_000); // TODO: Choose the number.
    let newStorageCanister = await CanDBPartition.CanDBPartition({
      partitionKey = pk;
      scalingOptions = {
        autoScalingHook = autoScaleCanister;
        sizeLimit = maxSize;
      };
      owners = ?controllers;
    });
    let newStorageCanisterPrincipal = Principal.fromActor(newStorageCanister);
    await CA.updateCanisterSettings({
      canisterId = newStorageCanisterPrincipal;
      settings = {
        controllers = ?controllers;
        compute_allocation = ?0;
        memory_allocation = ?0;
        freezing_threshold = ?2592000;
      }
    });

    let newStorageCanisterId = Principal.toText(newStorageCanisterPrincipal);
    pkToCanisterMap := CanisterMap.add(pkToCanisterMap, pk, newStorageCanisterId);

    Debug.print("new storage canisterId=" # newStorageCanisterId);
    newStorageCanisterId;
  };

  /// Put to a DB. It does not ensure no duplicates.
  public shared({caller}) func putWithHint(pk: Entity.PK, options: CanDB.PutOptions, hint: ?Principal): async () {
    checkCaller(caller);

    let partition = await* getExistingOrNewCanister(pk, options, hint);
    await partition.put(options);
  };

  // FIXME: race conditions?
  /// This function may be slow, because it tries all canisters in a partition, if `hint == null`.
  public shared({caller}) func putNoDuplicates(pk: Entity.PK, options: CanDB.PutOptions, hint: ?Principal): async () {
    checkCaller(caller);

    let partition = await* getExistingOrNewCanister(pk, options, hint);
    await partition.put({sk = options.sk; attributes = options.attributes});
  };

  public shared({caller}) func transformWithHint({
    pk: Entity.PK;
    sk: Entity.SK;
    modifier: shared(value: ?Entity.AttributeMap) -> async [(Entity.AttributeKey, Entity.AttributeValue)];
    hint: ?Principal;
  }): async () {
    checkCaller(caller);
 
    let partition = await* lastCanister(pk);
    let all = do ? { (await partition.get({sk}))!.attributes };
    await partition.put({sk; attributes = await modifier(all)});
  };

  func _transformAttribute({
    partition: CanDBPartition.CanDBPartition;
    sk: Entity.SK;
    subkey: Text;
    modifier: shared(value: ?Entity.AttributeValue) -> async Entity.AttributeValue;
  }): async* () {
    // TODO: duplicate code
    let all = do ? { (await partition.get({sk}))!.attributes };
    let new = switch (all) {
      case (?all) {
        let filtered = Iter.filter<(Entity.AttributeKey, Entity.AttributeValue)>(
          RBT.entries<Entity.AttributeKey, Entity.AttributeValue>(all),
          func((k,v): (Entity.AttributeKey, Entity.AttributeValue)) { k != subkey });
        let filteredArray = Iter.toArray(filtered);
        let newAll = Buffer.fromArray<(Entity.AttributeKey, Entity.AttributeValue)>(filteredArray);
        let old = RBT.get(all, Text.compare, subkey);
        let modified = await modifier(old);
        Buffer.add(newAll, (subkey, modified));
        Buffer.toArray(newAll);
      };
      case null {
        let modified = await modifier(null);
        [(subkey, modified)];
      };
    };
    await partition.put({sk; attributes = new});
  };

  public shared({caller}) func putAttrubuteWithHint({
    pk: Entity.PK;
    sk: Entity.SK;
    subkey: Text;
    value: Entity.AttributeValue;
    hint: ?Principal;
  }): async Principal {
    checkCaller(caller);
 
    let partition = await* lastCanister(pk);
    // TODO: duplicate code
    let all = do ? { (await partition.get({sk}))!.attributes };
    let new = switch (all) {
      case (?all) {
        let filtered = Iter.filter<(Entity.AttributeKey, Entity.AttributeValue)>(
          RBT.entries<Entity.AttributeKey, Entity.AttributeValue>(all),
          func((k,v): (Entity.AttributeKey, Entity.AttributeValue)) { k != subkey });
        let filteredArray = Iter.toArray(filtered);
        let newAll = Buffer.fromArray<(Entity.AttributeKey, Entity.AttributeValue)>(filteredArray);
        let old = RBT.get(all, Text.compare, subkey);
        Buffer.add(newAll, (subkey, value));
        Buffer.toArray(newAll);
      };
      case null {
        [(subkey, value)];
      };
    };
    await partition.put({sk; attributes = new});
    Principal.fromActor(partition);
  };

  public shared({caller}) func transformAttrubuteWithHint({
    pk: Entity.PK;
    sk: Entity.SK;
    subkey: Text;
    modifier: shared(value: ?Entity.AttributeValue) -> async Entity.AttributeValue;
    hint: ?Principal;
  }): async () {
    checkCaller(caller);
 
    let partition = await* lastCanister(pk);
    await* _transformAttribute({partition; sk; subkey; modifier});
  };

  public shared({caller}) func putAttrubuteNoDuplicates({
    pk: Entity.PK;
    sk: Entity.SK;
    subkey: Text;
    value: Entity.AttributeValue;
    hint: ?Principal;
  }): async () {
    checkCaller(caller);
 
    let partition = await* getExistingOrNewCanister(pk, {sk}, hint);
    // TODO: duplicate code
    let all = do ? { (await partition.get({sk}))!.attributes };
    let new = switch (all) {
      case (?all) {
        let filtered = Iter.filter<(Entity.AttributeKey, Entity.AttributeValue)>(
          RBT.entries<Entity.AttributeKey, Entity.AttributeValue>(all),
          func((k,v): (Entity.AttributeKey, Entity.AttributeValue)) { k != subkey });
        let filteredArray = Iter.toArray(filtered);
        let newAll = Buffer.fromArray<(Entity.AttributeKey, Entity.AttributeValue)>(filteredArray);
        let old = RBT.get(all, Text.compare, subkey);
        Buffer.add(newAll, (subkey, value));
        Buffer.toArray(newAll);
      };
      case null {
        [(subkey, value)];
      };
    };
    await partition.put({sk; attributes = new});
  };

  public shared({caller}) func transformAttrubuteNoDuplicates({
    pk: Entity.PK;
    sk: Entity.SK;
    subkey: Text;
    modifier: shared(value: ?Entity.AttributeValue) -> async Entity.AttributeValue;
    hint: ?Principal;
  }): async () {
    checkCaller(caller);
 
    let partition = await* getExistingOrNewCanister(pk, {sk}, hint);
    await* _transformAttribute({partition; sk; subkey; modifier});
  };

  /// This function may be slow, because it tries all canisters in a partition, if `hint == null`.
  public shared({caller}) func transformNoDuplicates({
    pk: Entity.PK;
    sk: Entity.SK;
    modifier: shared(value: ?Entity.AttributeMap) -> async [(Entity.AttributeKey, Entity.AttributeValue)];
    hint: ?Principal;
  }): async () {
    checkCaller(caller);
 
    let partition = await* getExistingOrNewCanister(pk, {sk}, hint);
    let all = do ? { (await partition.get({sk}))!.attributes };
    await partition.put({sk; attributes = await modifier(all)});
  };

  // Private functions for getting canisters //

  func lastCanister(pk: Entity.PK): async* CanDBPartition.CanDBPartition {
    let canisterIds = getCanisterIdsIfExists(pk);
    let part0 = if (canisterIds == []) {
      await* createStorageCanister(pk, ownersOrSelf());
    } else {
      canisterIds[canisterIds.size() - 1];
    };
    actor(part0);
  };

  /// This function may be slow, because it tries all canisters in a partition, if `hint == null`.
  func getExistingOrNewCanister(pk: Entity.PK, options: CanDB.GetOptions, hint: ?Principal): async* CanDBPartition.CanDBPartition {
    let existing = await* getExistingCanister(pk, options, hint);
    switch (existing) {
      case (?existing) { existing };
      case null {
        let newStorageCanisterId = await* createStorageCanister(pk, ownersOrSelf());
        actor(newStorageCanisterId);
      }
    }
  };

  func getExistingCanister(pk: Entity.PK, options: CanDB.GetOptions, hint: ?Principal): async* ?CanDBPartition.CanDBPartition {
    switch (hint) {
      case (?hint) {
        let canister: CanDBPartition.CanDBPartition = actor(Principal.toText(hint));
        if (await canister.skExists(options.sk)) {
          return ?canister;
        } else {
          Debug.trap("wrong DB partition hint");
        };
      };
      case null {};
    };

    // Do parallel search in existing canisters:
    let canisterIds = getCanisterIdsIfExists(pk);
    let threads : [var ?(async())] = Array.init(canisterIds.size(), null);
    var foundInCanister: ?Nat = null;
    for (threadNum in threads.keys()) {
      threads[threadNum] := ?(async {
        let canister: CanDBPartition.CanDBPartition = actor(canisterIds[threadNum]);
        switch (foundInCanister) {
          case (?foundInCanister) {
            if (foundInCanister < threadNum) {
              return; // eliminate unnecessary work.
            };
          };
          case null {};
        };
        if (await canister.skExists(options.sk)) {
          foundInCanister := ?threadNum;
        };
      });
    };
    for (topt in threads.vals()) {
      let ?t = topt else {
        Debug.trap("programming error: threads");
      };
      await t;
    };

    switch (foundInCanister) {
      case (?foundInCanister) {
        ?(actor(canisterIds[foundInCanister]): CanDBPartition.CanDBPartition);
      };
      case null {
        let newStorageCanisterId = await* createStorageCanister(pk, ownersOrSelf());
        ?(actor(newStorageCanisterId): CanDBPartition.CanDBPartition);
      };
    };
  };
}